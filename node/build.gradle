apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: QuasarPlugin
apply plugin: DefaultPublishTasks

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    jcenter()
    maven {
        url 'https://dl.bintray.com/kotlin/exposed'
    }
}


//noinspection GroovyAssignabilityCheck
configurations {
    // we don't want isolated.jar in classPath, since we want to test jar being dynamically loaded as an attachment
    runtime.exclude module: 'isolated'

    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

// Force the Caplet to target Java 6. This ensures that running 'java -jar corda.jar' on any Java 6 VM upwards
// will get as far as the Capsule version checks, meaning that if your JVM is too old, you will at least get
// a sensible error message telling you what to do rather than a bytecode version exception that doesn't.
// If we introduce .java files into this module that need Java 8+ then we will have to push the caplet into
// its own module so its target can be controlled individually, but for now this suffices.
sourceCompatibility = 1.6
targetCompatibility = 1.6

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/kotlin')
        }
    }
    test {
        resources {
            srcDir "../config/test"
        }
    }
}

// To find potential version conflicts, run "gradle htmlDependencyReport" and then look in
// build/reports/project/dependencies/index.html for green highlighted parts of the tree.

dependencies {
    compile project(':finance')
    testCompile project(':test-utils')

    compile "com.google.code.findbugs:jsr305:3.0.1"

    // Log4J: logging framework (with SLF4J bindings)
    compile "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    compile "org.apache.logging.log4j:log4j-core:${log4j_version}"
    compile "org.apache.logging.log4j:log4j-web:${log4j_version}"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    compile "com.google.guava:guava:19.0"

    // JOpt: for command line flags.
    compile "net.sf.jopt-simple:jopt-simple:5.0.2"

    // Artemis: for reliable p2p message queues.
    compile "org.apache.activemq:artemis-server:${artemis_version}"
    compile "org.apache.activemq:artemis-core-client:${artemis_version}"
    runtime "org.apache.activemq:artemis-amqp-protocol:${artemis_version}"

    // JAnsi: for drawing things to the terminal in nicely coloured ways.
    compile "org.fusesource.jansi:jansi:1.13"

    // GraphStream: For visualisation
    testCompile "org.graphstream:gs-core:1.3"
    testCompile("org.graphstream:gs-ui:1.3") {
        exclude group: "bouncycastle"
    }
    compile("com.intellij:forms_rt:7.0.3") {
        exclude group: "asm"
    }

    // Force commons logging to version 1.2 to override Artemis, which pulls in 1.1.3 (ARTEMIS-424)
    compile "commons-logging:commons-logging:1.2"

    // Web stuff: for HTTP[S] servlets
    compile "org.eclipse.jetty:jetty-servlet:${jetty_version}"
    compile "org.eclipse.jetty:jetty-webapp:${jetty_version}"
    compile "javax.servlet:javax.servlet-api:3.1.0"
    compile "org.jolokia:jolokia-agent-war:2.0.0-M1"
    compile "commons-fileupload:commons-fileupload:1.3.2"

    // Jersey for JAX-RS implementation for use in Jetty
    compile "org.glassfish.jersey.core:jersey-server:${jersey_version}"
    compile "org.glassfish.jersey.containers:jersey-container-servlet-core:${jersey_version}"
    compile "org.glassfish.jersey.containers:jersey-container-jetty-http:${jersey_version}"
    // NOTE there is a Jackson version clash between jersey-media-json-jackson (v2.5.4) and jackson-module-kotlin (v.2.5.5)
    // Have not found an Issue in the issue tracker for Jersey for this issue
    compile ("org.glassfish.jersey.media:jersey-media-json-jackson:${jersey_version}") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    }
    compile ("com.fasterxml.jackson.module:jackson-module-kotlin:${jackson_version}") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
    }
    compile "com.fasterxml.jackson.core:jackson-annotations:${jackson_version}"

    // Coda Hale's Metrics: for monitoring of key statistics
    compile "io.dropwizard.metrics:metrics-core:3.1.2"

    // JimFS: in memory java.nio filesystem. Used for test and simulation utilities.
    compile "com.google.jimfs:jimfs:1.1"

    // TypeSafe Config: for simple and human friendly config files.
    compile "com.typesafe:config:1.3.0"

    // Unit testing helpers.
    testCompile 'junit:junit:4.12'
    testCompile "org.assertj:assertj-core:${assertj_version}"

    testCompile 'com.pholser:junit-quickcheck-core:0.6'

    // For H2 database support in persistence
    compile "com.h2database:h2:1.4.192"

    // Exposed: Kotlin SQL library - under evaluation
    compile "org.jetbrains.exposed:exposed:0.5.0"

    // SQL connection pooling library
    compile "com.zaxxer:HikariCP:2.4.7"

    // Hibernate: an object relational mapper for writing state objects to the database automatically.
    compile "org.hibernate:hibernate-core:5.2.2.Final"
    compile "org.hibernate:hibernate-java8:5.2.2.Final"

    // Capsule is a library for building independently executable fat JARs.
    compile 'co.paralleluniverse:capsule:1.0.3'

    // Java Atomix: RAFT library
    compile 'io.atomix:atomix-all:1.0.0-rc4'

    // Integration test helpers
    integrationTestCompile 'junit:junit:4.12'

    testCompile "com.nhaarman:mockito-kotlin:0.6.1"
}

quasarScan.dependsOn('classes', ':core:classes', ':finance:classes')

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

publishing {
    publications {
        node(MavenPublication) {
            from components.java
            artifactId 'node'

            artifact sourceJar
            artifact javadocJar
        }
    }
}