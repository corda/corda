buildscript {
    ext.commonsio_version = '2.6'
    ext.commonslogging_version = '1.2'
    ext.cucumber_version = '1.2.5'
    ext.crash_version = 'cce5a00f114343c1145c1d7756e1dd6df3ea984e'
    ext.docker_client_version = '8.11.0'
    ext.kubernetes_client_version = '0.2'

    repositories {
        mavenLocal()
        maven {
            jcenter()
            url 'https://jitpack.io'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'net.corda.behave'

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    scenario {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/scenario/kotlin')
        }
        resources.srcDir file('src/scenario/resources')
    }
    smokeTest {
        kotlin {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smoke-test/kotlin')
        }
    }
    rpcProxy {
        kotlin {
            srcDir "src/main/kotlin"
        }
        resources {
            srcDir "config/dev"
        }
    }
}

configurations {
    scenarioCompile.extendsFrom testCompile
    scenarioRuntime.extendsFrom testRuntime

    smokeTestCompile.extendsFrom compile
    smokeTestRuntime.extendsFrom runtime
}

dependencies {

    // Library

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile("com.github.corda.crash:crash.shell:$crash_version") {
        exclude group: "org.slf4j", module: "slf4j-jdk14"
        exclude group: "org.bouncycastle"
    }

    compile("com.github.corda.crash:crash.connectors.ssh:$crash_version") {
        exclude group: "org.slf4j", module: "slf4j-jdk14"
        exclude group: "org.bouncycastle"
    }

    compile "org.slf4j:log4j-over-slf4j:$slf4j_version"
    compile "org.slf4j:jul-to-slf4j:$slf4j_version"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    compile "org.apache.logging.log4j:log4j-core:$log4j_version"

    compile "commons-io:commons-io:$commonsio_version"
    compile "commons-logging:commons-logging:$commonslogging_version"
    compile "com.spotify:docker-client:$docker_client_version"
    compile "io.kubernetes:client-java:$kubernetes_client_version"
    compile "io.reactivex:rxjava:$rxjava_version"

    compileOnly project(':finance')
    compileOnly project(':node-api')
    compileOnly project(':client:rpc')

    testCompile project(':test-utils')

    // CorDapps (TODO: move to separate repo)
    compile project(':samples:simm-valuation-demo')
    // TODO: introduce a plugin mechanism to classload 3rd party apps.
//    compile files('/Users/josecoll/.m2/repository/net/corda/option/cordapp-option/0.3/cordapp-option-0.3.jar')
//    compileOnly "net.corda.option:cordapp-option:0.1"

    // Unit Tests

    testCompile "junit:junit:$junit_version"
    testCompile "org.assertj:assertj-core:$assertj_version"

    // Scenarios / End-to-End Tests

    scenarioCompile "info.cukes:cucumber-java8:$cucumber_version"
    scenarioCompile "info.cukes:cucumber-junit:$cucumber_version"
    scenarioCompile "info.cukes:cucumber-picocontainer:$cucumber_version"

    // Smoke tests
    smokeTestCompile "org.assertj:assertj-core:${assertj_version}"
    smokeTestCompile "junit:junit:$junit_version"

    // Jetty http server
    rpcProxyCompile "org.eclipse.jetty:jetty-servlet:$jetty_version"
    rpcProxyCompile "org.eclipse.jetty:jetty-webapp:$jetty_version"
    rpcProxyCompile "javax.servlet:javax.servlet-api:3.1.0"

    // Jersey for JAX-RS implementation for use in Jetty
    rpcProxyCompile "org.glassfish.jersey.core:jersey-server:$jersey_version"
    rpcProxyCompile "org.glassfish.jersey.containers:jersey-container-servlet-core:$jersey_version"
    rpcProxyCompile "org.glassfish.jersey.containers:jersey-container-jetty-http:$jersey_version"

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileScenarioKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    testLogging.showStandardStreams = true
}

task scenarios(type: Test) {
    setTestClassesDirs sourceSets.scenario.output.getClassesDirs()
    classpath = sourceSets.scenario.runtimeClasspath
    outputs.upToDateWhen { false }

    if (project.hasProperty("tags")) {
        systemProperty "cucumber.options", "--tags $tags"
        logger.warn("Only running tests tagged with: $tags ...")
    }
}

task smokeTest(type: Test) {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

task rpcProxyJar(type: Jar) {
    from {
        configurations.rpcProxyRuntime.collect { it.isDirectory() ? it : zipTree(it) }
    }
//    zip64 true
    with jar
    exclude("META-INF/*.DSA")
    exclude("META-INF/*.RSA")
    exclude("META-INF/*.SF")
    manifest {
        attributes 'Main-Class': 'net.corda.behave.service.proxy.RPCProxyServerKt'
    }
    archiveName "corda-rpcProxy.jar"
}

//scenarios.mustRunAfter test
//scenarios.dependsOn test