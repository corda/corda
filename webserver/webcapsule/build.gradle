/**
 * This build.gradle exists to publish our capsule (executable fat jar) to maven. It cannot be placed in the
 * webserver project because the bintray plugin cannot publish two modules from one project.
 */
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'us.kirchmeier.capsule'
apply plugin: 'com.jfrog.artifactory'

description 'Corda node web server capsule'

configurations {
    runtimeArtifacts
    capsuleRuntime
}

dependencies {
    // TypeSafe Config: for simple and human friendly config files.
    capsuleRuntime "com.typesafe:config:$typesafe_config_version"
}

// Force the Caplet to target Java 6. This ensures that running 'java -jar corda.jar' on any Java 6 VM upwards
// will get as far as the Capsule version checks, meaning that if your JVM is too old, you will at least get
// a sensible error message telling you what to do rather than a bytecode version exception that doesn't.
// If we introduce .java files into this module that need Java 8+ then we will have to push the caplet into
// its own module so its target can be controlled individually, but for now this suffices.
sourceCompatibility = 1.6
targetCompatibility = 1.6

jar.enabled = false

task buildWebserverJar(type: FatCapsule, dependsOn: project(':node').tasks.jar) {
    applicationClass 'net.corda.webserver.WebServer'
    archiveName "corda-webserver-${corda_release_version}.jar"
    applicationSource = files(
        project(':webserver').configurations.runtimeClasspath,
        project(':webserver').tasks.jar,
        project(':node').sourceSets.main.java.outputDir.toString() + '/CordaCaplet.class',
        project(':node').sourceSets.main.java.outputDir.toString() + '/CordaCaplet$1.class',
        project(':node').buildDir.toString() + '/resources/main/reference.conf',
        "$rootDir/config/dev/log4j2.xml",
        project(':node:capsule').projectDir.toString() + '/NOTICE' // Copy CDDL notice
    )
    from configurations.capsuleRuntime.files.collect { zipTree(it) }

    capsuleManifest {
        applicationVersion = corda_release_version
        javaAgents = ["quasar-core-${quasar_version}-jdk8.jar"]
        systemProperties['visualvm.display.name'] = 'Corda Webserver'
        minJavaVersion = '1.8.0'
        minUpdateVersion['1.8'] = java8_minUpdateVersion
        caplets = ['CordaCaplet']

        // JVM configuration:
        // - Constrain to small heap sizes to ease development on low end devices.
        // - Switch to the G1 GC which is going to be the default in Java 9 and gives low pause times/string dedup.
        //
        // If you change these flags, please also update Driver.kt
        jvmArgs = ['-Xmx200m', '-XX:+UseG1GC']
    }
}

assemble.dependsOn buildWebserverJar

artifacts {
    runtimeArtifacts buildWebserverJar
    publish buildWebserverJar {
        classifier ''
    }
}

publish {
    disableDefaultJar = true
    name 'corda-webserver'
}
