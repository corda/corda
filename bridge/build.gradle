apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'net.corda.plugins.quasar-utils'

description 'Corda peer bridging components'

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/kotlin')
        }
        resources {
            srcDir file('src/integration-test/resources')
        }
    }
}

processResources {
    from file("$rootDir/config/dev/log4j2.xml")
}

dependencies {

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    compile(project(':core')) {
        transitive = false // we control dependencies directly as the bridge is likely to be audited
    }

    compile(project(':common-configuration-parsing')) {
        transitive = false // we control dependencies directly as the bridge is likely to be audited
    }

    compile(project(':node-api')) {
        transitive = false// we control dependencies directly as the bridge is likely to be audited
    }

    compile(project(':serialization')) {
        transitive = false// we control dependencies directly as the bridge is likely to be audited
    }

    compile(project(':tools:cliutils')) {
        transitive = false// we control dependencies directly as the bridge is likely to be audited
    }

    // For HSM simulator
    testCompile project(':node-api').sourceSets.test.output
    testCompile project(':test-cli')
    testCompile fileTree(dir: 'lib', include: 'CryptoServer*.jar')
    // Spotify Docker client for using docker containers in integration tests
    testCompile "com.spotify:docker-client:8.14.5"

    // Here we pull in dependencies that would normally be pulled in transitively from :core and :node-api, but we need more fine grained control
    // For AMQP serialisation.
    compile "org.apache.qpid:proton-j:${protonj_version}"
    compile "com.google.guava:guava:$guava_version"
    // RxJava: observable streams of events.
    compile "io.reactivex:rxjava:$rxjava_version"
    compile("org.apache.activemq:artemis-core-client:${artemis_version}")
    compile "org.apache.activemq:artemis-commons:${artemis_version}"
    compile "io.netty:netty-handler-proxy:$netty_version"
    // TypeSafe Config: for simple and human friendly config files.
    compile "com.typesafe:config:$typesafe_config_version"

    // The following dependencies are required to load and deserialize network-info, or other static initializers that get triggered.
    // Java ed25519 implementation. See https://github.com/str4d/ed25519-java/ for deserializing our public keys and certs.
    compile "net.i2p.crypto:eddsa:$eddsa_version"
    // Bouncy castle support needed for X509 certificate manipulation
    compile "org.bouncycastle:bcprov-jdk15on:${bouncycastle_version}"
    compile "org.bouncycastle:bcpkix-jdk15on:${bouncycastle_version}"
    // Needed for CRLs caching
    compile "com.github.ben-manes.caffeine:caffeine:$caffeine_version"

    // Log4J: logging framework (with SLF4J bindings)
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    compile "org.apache.logging.log4j:log4j-core:$log4j_version"
    compile "org.slf4j:jul-to-slf4j:$slf4j_version"
    compile "org.slf4j:jcl-over-slf4j:$slf4j_version"
    compile "com.lmax:disruptor:$disruptor_version"

    // Manifests: for reading stuff from the manifest file
    compile "com.jcabi:jcabi-manifests:1.1"

    // Picocli: for parsing command line options
    compile "info.picocli:picocli:$picocli_version"

    // JAnsi: for drawing things to the terminal in nicely coloured ways.
    compile "org.fusesource.jansi:jansi:$jansi_version"

    compile "commons-lang:commons-lang:$commons_lang_version"

    integrationTestCompile project(':node-driver')
    integrationTestCompile "org.apache.curator:curator-test:${curator_version}"
    testCompile "junit:junit:$junit_version"
    testCompile "org.apache.curator:curator-test:${curator_version}"
    testCompile project(':test-utils')
    testCompile project(':tools:ha-utilities')
    integrationTestCompile project(':tools:ha-utilities')

    // Capsule is a library for building independently executable fat JARs.
    // We only need this dependency to compile our Caplet against.
    compileOnly "co.paralleluniverse:capsule:$capsule_version"
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    systemProperty 'testing.global.port.allocation.enabled', true
    systemProperty 'testing.global.port.allocation.starting.port', 20000
}

jar {
    baseName 'corda-bridge-impl'
}

publish {
    name jar.baseName
}
