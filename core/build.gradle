apply plugin: 'kotlin'
apply plugin: 'kotlin-jpa'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.publish-utils'

description 'Corda core'

buildscript {
    repositories {
        mavenCentral()
    }
}

dependencies {

    testCompile "junit:junit:$junit_version"
    testCompile "commons-fileupload:commons-fileupload:$fileupload_version"

    // Guava: Google test library (collections test suite)
    testCompile "com.google.guava:guava-testlib:$guava_version"

    // Bring in the MockNode infrastructure for writing protocol unit tests.
    testCompile project(":node")
    testCompile project(":test-utils")

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    // Thread safety annotations
    compile "com.google.code.findbugs:jsr305:3.0.1"

    // Log4J: logging framework (ONLY explicitly referenced by net.corda.core.utilities.Logging.kt)
    compile "org.apache.logging.log4j:log4j-core:${log4j_version}"

    // SLF4J: commons-logging bindings for a SLF4J back end
    compile "org.slf4j:jcl-over-slf4j:$slf4j_version"
    compile "org.slf4j:slf4j-api:$slf4j_version"

    // AssertJ: for fluent assertions for testing
    testCompile "org.assertj:assertj-core:${assertj_version}"

    // TODO: Upgrade to junit-quickcheck 0.8, once it is released,
    //       because it depends on org.javassist:javassist instead
    //       of javassist:javassist.
    testCompile "com.pholser:junit-quickcheck-core:$quickcheck_version"
    testCompile "com.pholser:junit-quickcheck-generators:$quickcheck_version"

    // Guava: Google utilities library.
    compile "com.google.guava:guava:$guava_version"

    // RxJava: observable streams of events.
    compile "io.reactivex:rxjava:$rxjava_version"

    // Kryo: object graph serialization.
    compile "com.esotericsoftware:kryo:4.0.0"
    compile "de.javakaffee:kryo-serializers:0.41"

    // Apache JEXL: An embeddable expression evaluation library.
    // This may be temporary until we experiment with other ways to do on-the-fly contract specialisation via an API.
    compile "org.apache.commons:commons-jexl3:3.0"

    // For JSON
    compile "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"

    // Java ed25519 implementation. See https://github.com/str4d/ed25519-java/
    compile "net.i2p.crypto:eddsa:$eddsa_version"

    // Bouncy castle support needed for X509 certificate manipulation
    compile "org.bouncycastle:bcprov-jdk15on:${bouncycastle_version}"
    compile "org.bouncycastle:bcpkix-jdk15on:${bouncycastle_version}"

    // JPA 2.1 annotations.
    compile "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final"

    // Requery: SQL based query & persistence for Kotlin
    compile "io.requery:requery-kotlin:$requery_version"

    // For AMQP serialisation.
    compile "org.apache.qpid:proton-j:0.19.0"
}

configurations {
    testArtifacts.extendsFrom testRuntime
}

task testJar(type: Jar) {
    classifier "tests"
    from sourceSets.test.output
}

artifacts {
    testArtifacts testJar
}

jar {
    baseName 'corda-core'
}

publish {
    name = jar.baseName
}
