apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.api-scanner'
apply plugin: 'com.jfrog.artifactory'

description 'Corda client RPC modules'

//noinspection GroovyAssignabilityCheck
configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly

    smokeTestCompile.extendsFrom compile
    smokeTestRuntimeOnly.extendsFrom runtimeOnly
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/kotlin')
        }
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources {
            srcDirs "src/integration-test/resources"
        }
    }
    smokeTest {
        kotlin {
            // We must NOT have any Node code on the classpath, so do NOT
            // include the test or integrationTest dependencies here.
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smoke-test/kotlin')
        }
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smoke-test/java')
        }
    }
}

processSmokeTestResources {
    from(project(':node:capsule').tasks['buildCordaJAR']) {
        rename 'corda-(.*)', 'corda.jar'
    }
    from(project(':finance:workflows').tasks['jar']) {
        rename '.*finance-workflows-.*', 'cordapp-finance-workflows.jar'
    }
    from(project(':finance:contracts').tasks['jar']) {
        rename '.*finance-contracts-.*', 'cordapp-finance-contracts.jar'
    }
    from(project(':testing:cordapps:sleeping').tasks['jar']) {
        rename 'testing-sleeping-cordapp-*', 'cordapp-sleeping.jar'
    }
}

// To find potential version conflicts, run "gradle htmlDependencyReport" and then look in
// build/reports/project/dependencies/index.html for green highlighted parts of the tree.

dependencies {
    implementation project(':core')
    implementation project(':node-api')
    implementation project(':serialization')

    // For caches rather than guava
    implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testImplementation "junit:junit:$junit_version"

    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junit_vintage_version}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junit_platform_version}"

    // Unit testing helpers.
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.assertj:assertj-core:${assertj_version}"
    testImplementation "io.dropwizard.metrics:metrics-core:$metrics_version"

    testImplementation project(':node')
    testImplementation project(':node-driver')
    testImplementation project(':client:mock')
    testImplementation project(':core-test-utils')


    integrationTestImplementation project(path: ':node-api', configuration: 'testArtifacts')
    integrationTestImplementation project(':common-configuration-parsing')
    integrationTestImplementation project(':finance:contracts')
    integrationTestImplementation project(':finance:workflows')
    integrationTestImplementation project(':test-utils')

    integrationTestImplementation "co.paralleluniverse:quasar-core:$quasar_version"
    integrationTestImplementation "org.mockito.kotlin:mockito-kotlin:$mockito_kotlin_version"

    // Smoke tests do NOT have any Node code on the classpath!
    smokeTestCompile project(':core')
    smokeTestCompile project(':node-api')
    smokeTestCompile project(':smoke-test-utils')
    smokeTestCompile project(':finance:contracts')
    smokeTestCompile project(':finance:workflows')
    smokeTestCompile project(':testing:cordapps:sleeping')
    smokeTestCompile "io.reactivex:rxjava:$rxjava_version"
    smokeTestCompile "commons-io:commons-io:$commons_io_version"
    smokeTestCompile "org.hamcrest:hamcrest-library:2.1"
    smokeTestCompile "com.google.guava:guava-testlib:$guava_version"
    smokeTestCompile "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    smokeTestCompile "org.apache.logging.log4j:log4j-core:$log4j_version"
    smokeTestCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    smokeTestCompile "org.assertj:assertj-core:${assertj_version}"
    smokeTestImplementation "junit:junit:$junit_version"
    smokeTestRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junit_vintage_version}"
    smokeTestRuntimeOnly "org.junit.platform:junit-platform-launcher:${junit_platform_version}"

    // JDK11: required by Quasar at run-time
    smokeTestRuntimeOnly "com.esotericsoftware:kryo:$kryo_version"

    implementation "io.reactivex:rxjava:$rxjava_version"
    implementation("org.apache.activemq:artemis-core-client:${artemis_version}") {
        exclude group: 'org.jgroups', module: 'jgroups'
    }
    implementation "com.google.guava:guava-testlib:$guava_version"
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task smokeTest(type: Test) {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

jar {
    baseName 'corda-rpc'
    manifest {
        attributes 'Automatic-Module-Name': 'net.corda.client.rpc'
    }
}

publish {
    name jar.baseName
}
