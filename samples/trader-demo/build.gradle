apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'

cordapp {
    info {
        name "Trader Demo"
        vendor "R3"
        targetPlatformVersion corda_platform_version.toInteger()
        minimumPlatformVersion 1
    }
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/kotlin')
        }
        resources {
            srcDir file('src/integration-test/resources')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    if (System.getProperty('excludeShell') == null) {
        cordaDriver "net.corda:corda-shell:$corda_release_version"
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "net.sf.jopt-simple:jopt-simple:$jopt_simple_version"
    cordaCompile project(':client:rpc')

    // Cordformation needs a SLF4J implementation when executing the Network
    // Bootstrapper, but Log4J doesn't shutdown completely from within Gradle.
    // Use a much simpler SLF4J implementation here instead.
    cordaRuntime "org.slf4j:slf4j-simple:$slf4j_version"

    // We only need this for its DUMMY_BANK constants, and
    // DO NOT want it added to Gradle's runtime classpath.
    compileOnly project(':test-utils')

    // The trader demo CorDapp depends upon Cash CorDapp features
    cordapp project(':finance:contracts')
    cordapp project(':finance:workflows')
    cordapp project(':samples:trader-demo:workflows-trader')

    // Corda integration dependencies
    cordaRuntime project(path: ":node:capsule", configuration: 'runtimeArtifacts')

    testCompile "org.slf4j:slf4j-simple:$slf4j_version"
    testCompile(project(':node-driver')) {
        // We already have a SLF4J implementation on our runtime classpath,
        // and we don't need another one.
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    }
    
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_jupiter_version}"
    testImplementation "junit:junit:$junit_version"

    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junit_vintage_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_jupiter_version}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junit_platform_version}"

    testCompile "org.assertj:assertj-core:$assertj_version"
}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

def nodeTask = tasks.getByPath(':node:capsule:assemble')
task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar', nodeTask]) {
    nodeDefaults {
        projectCordapp {
            deploy = false // TODO This is a bug, project cordapp should be disabled if no cordapp plugin is applied.
        }
        rpcUsers = [['username': "demo", 'password': "demo", 'permissions': ["ALL"]]]
        cordapp project(':finance:workflows')
        cordapp project(':finance:contracts')
        cordapp project(':samples:trader-demo:workflows-trader')
        runSchemaMigration = true
    }
    node {
        name "O=Notary Node,L=Zurich,C=CH"
        notary = [validating : true,
                  serviceLegalName: "O=Notary Service,L=Zurich,C=CH"
        ]
        p2pPort 10002
        rpcUsers = []
        rpcSettings {
            address "localhost:10003"
            adminAddress "localhost:10004"
        }
        extraConfig = ['h2Settings.address' : 'localhost:10014']
    }
    node {
        name "O=Bank A,L=London,C=GB"
        p2pPort 10005
        rpcSettings {
            address "localhost:10006"
            adminAddress "localhost:10007"
        }
        extraConfig = ['h2Settings.address' : 'localhost:10015']
    }
    node {
        name "O=Bank B,L=New York,C=US"
        p2pPort 10008
        rpcSettings {
            address "localhost:10009"
            adminAddress "localhost:10010"
        }
        extraConfig = ['h2Settings.address' : 'localhost:10016']
    }
    node {
        name "O=BankOfCorda,L=New York,C=US"
        p2pPort 10011
        rpcSettings {
            address "localhost:10012"
            adminAddress "localhost:10013"
        }
        extraConfig = ['h2Settings.address' : 'localhost:10017']
    }
    
    //All other nodes should be using LoggingBuyerFlow as it is a subclass of BuyerFlow
    node {
        name "O=NonLogging Bank,L=London,C=GB"
        p2pPort 10025
        rpcSettings {
            address "localhost:10026"
            adminAddress "localhost:10027"
        }
        extraConfig = ['h2Settings.address' : 'localhost:10035']
        flowOverride("net.corda.traderdemo.flow.SellerFlow", "net.corda.traderdemo.flow.BuyerFlow")
    }
}

idea {
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}

task runBank(type: JavaExec, dependsOn: jar) {
    classpath = sourceSets.test.runtimeClasspath
    main = 'net.corda.traderdemo.TraderDemoKt'
    args '--role'
    args 'BANK'
}

task runSeller(type: JavaExec, dependsOn: jar) {
    classpath = sourceSets.test.runtimeClasspath
    main = 'net.corda.traderdemo.TraderDemoKt'
    args '--role'
    args 'SELLER'
}
