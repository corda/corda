apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'https://dl.bintray.com/kotlin/exposed'
    }
}

sourceSets {
    main {
        resources {
            srcDir "../../config/dev"
        }
    }
    test {
        resources {
            srcDir "../../config/test"
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/kotlin')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "junit:junit:$junit_version"

    // Corda integration dependencies
    runtime project(path: ":node:capsule", configuration: 'runtimeArtifacts')
    compile project(':core')
    compile project(':client')
    compile project(':node')
    compile project(':finance')
    compile project(':test-utils')

    // Javax is required for webapis
    compile "org.glassfish.jersey.core:jersey-server:${jersey_version}"
}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['build']) {
    directory "./build/nodes"
    // This name "Notary" is hard-coded into BankOfCordaClientApi so if you change it here, change it there too.
    // In this demo the node that runs a standalone notary also acts as the network map server.
    networkMap "Notary"
    node {
        name "Notary"
        nearestCity "London"
        advertisedServices = ["corda.notary.validating"]
        artemisPort 10002
        webPort 10003
        cordapps = []
    }
    node {
        name "BankOfCorda"
        nearestCity "London"
        advertisedServices = ["corda.issuer.USD"]
        artemisPort 10004
        webPort 10005
        cordapps = []
        rpcUsers = [
                ['user' : "user1",
                 'password' : "test",
                 'permissions' : ["StartFlow.net.corda.flows.CashFlow",
                                  "StartFlow.net.corda.flows.IssuerFlow\$IssuanceRequester"]]
        ]
    }
    node {
        name "BigCorporation"
        nearestCity "New York"
        advertisedServices = []
        artemisPort 10006
        webPort 10007
        cordapps = []
        rpcUsers = [
                ['user' : "user1",
                 'password' : "test",
                 'permissions' : ["StartFlow.net.corda.flows.CashFlow"]]
        ]
    }
}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

idea {
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}

publishing {
    publications {
        jarAndSources(MavenPublication) {
            from components.java
            artifactId 'bankofcorda'

            artifact sourceJar
            artifact javadocJar
        }
    }
}

task runIssuer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.bank.BankOfCordaDriverKt'
    args '--role'
    args 'ISSUER'
}

task runRPCCashIssue(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.bank.BankOfCordaDriverKt'
    args '--role'
    args 'ISSUE_CASH_RPC'
    args '--quantity'
    args 20000
    args '--currency'
    args 'USD'
}

task runWebCashIssue(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.bank.BankOfCordaDriverKt'
    args '--role'
    args 'ISSUE_CASH_WEB'
    args '--quantity'
    args 30000
    args '--currency'
    args 'GBP'
}
