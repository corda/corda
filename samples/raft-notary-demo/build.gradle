apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'maven-publish'

ext {
    deployTo = "build/notary-demo-nodes"
    notaryType = "corda.notary.validating.raft"
    notaryName = "Raft"
    advertisedNotary = "$notaryType|$notaryName"
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    testCompile "junit:junit:$junit_version"

    // Corda integration dependencies
    runtime project(path: ":node:capsule", configuration: 'runtimeArtifacts')
    runtime project(path: ":webserver:webcapsule", configuration: 'runtimeArtifacts')
    compile project(':core')
    compile project(':client:jfx')
    compile project(':client:rpc')
    compile project(':test-utils')

    // Javax is required for webapis
    compile "org.glassfish.jersey.core:jersey-server:${jersey_version}"
}

idea {
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}

publishing {
    publications {
        jarAndSources(MavenPublication) {
            from components.java
            artifactId 'raftnotarydemo'

            artifact sourceJar
            artifact javadocJar
        }
    }
}

task cleanNodes {
    doLast {
        delete deployTo
    }
}

task generateNotaryIdentity(type: JavaExec, dependsOn: 'cleanNodes') {
    classpath = sourceSets.main.runtimeClasspath
    main = "net.corda.node.utilities.ServiceIdentityGeneratorKt"
    def nodeDirs = ["$deployTo/Notary1",
                    "$deployTo/Notary2",
                    "$deployTo/Notary3"].join("|")
    args = [nodeDirs, notaryType, notaryName]
}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar', 'generateNotaryIdentity']) {
    directory deployTo
    networkMap "CN=Notary 1,O=R3,OU=corda,L=London,C=UK"
    node {
        name "CN=Alice Corp,O=Alice Corp,L=London,C=UK"
        nearestCity "London"
        advertisedServices = []
        p2pPort 10001
        rpcPort 10002
        cordapps = []
        rpcUsers = [['username': "demo", 'password': "demo", 'permissions': [
                'StartFlow.net.corda.notarydemo.flows.DummyIssueAndMove',
                'StartFlow.net.corda.flows.NotaryFlow$Client'
        ]]]
    }
    node {
        name "CN=Bob Plc,O=Bob Plc,L=London,C=UK"
        nearestCity "New York"
        advertisedServices = []
        p2pPort 10004
        rpcPort 10005
        cordapps = []
    }
    node {
        name "CN=Notary 1,O=R3,OU=corda,L=London,C=UK"
        nearestCity "London"
        advertisedServices = [advertisedNotary]
        p2pPort 10008
        rpcPort 10009
        cordapps = []
        notaryNodePort 10007
    }
    node {
        name "CN=Notary 2,O=R3,OU=corda,L=London,C=UK"
        nearestCity "London"
        advertisedServices = [advertisedNotary]
        p2pPort 10012
        rpcPort 10013
        cordapps = []
        notaryNodePort 10011
        notaryClusterAddresses = ["localhost:10007"]
    }
    node {
        name "CN=Notary 3,O=R3,OU=corda,L=London,C=UK"
        nearestCity "London"
        advertisedServices = [advertisedNotary]
        p2pPort 10016
        rpcPort 10017
        cordapps = []
        notaryNodePort 10015
        notaryClusterAddresses = ["localhost:10007"]
    }
}

task notarise(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.notarydemo.NotaryDemoKt'
}

