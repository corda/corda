import net.corda.plugins.Cordform

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'maven-publish'

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    testCompile "junit:junit:$junit_version"

    // Corda integration dependencies
    compile project(path: ":node:capsule", configuration: 'runtimeArtifacts')
    compile project(path: ":webserver:webcapsule", configuration: 'runtimeArtifacts')
    compile project(':core')
    compile project(':client:jfx')
    compile project(':client:rpc')
    compile project(':test-utils')
    compile project(':cordform-common')

    // Javax is required for webapis
    compile "org.glassfish.jersey.core:jersey-server:${jersey_version}"
}

idea {
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}

publishing {
    publications {
        jarAndSources(MavenPublication) {
            from components.java
            artifactId 'notarydemo'

            artifact sourceJar
            artifact javadocJar
        }
    }
}

task deployNodes(dependsOn: ['deployNodesSingle', 'deployNodesRaft', 'deployNodesBFT'])

task deployNodesSingle(type: Cordform, dependsOn: 'jar') {
    directory "./build/nodes/nodesSingle"
    definitionClass = 'net.corda.notarydemo.SingleNotaryCordform'
}

task deployNodesRaft(type: Cordform, dependsOn: 'jar') {
    directory "./build/nodes/nodesRaft"
    definitionClass = 'net.corda.notarydemo.RaftNotaryCordform'
}

task deployNodesBFT(type: Cordform, dependsOn: 'jar') {
    directory "./build/nodes/nodesBFT"
    definitionClass = 'net.corda.notarydemo.BFTNotaryCordform'
}

task notarise(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.notarydemo.NotariseKt'
}
