apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.cordapp'

configurations {
    demoArtifacts.extendsFrom testRuntimeClasspath
}

dependencies {
    // The irs demo CorDapp depends upon Cash CorDapp features
    cordapp project(':finance:contracts')
    cordapp project(':finance:workflows')

    // Corda integration dependencies
    cordaCompile project(':core')

    compile("com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version")
    
    // only included to control the `DemoClock` as part of the demo application
    // normally `:node` should not be depended on in any CorDapps
    compileOnly project(':node')

    // Cordapp dependencies
    // Specify your cordapp's dependencies below, including dependent cordapps
    compile "commons-io:commons-io:$commons_io_version"

    testCompile project(':node-driver')
    
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_jupiter_version}"
    testImplementation "junit:junit:$junit_version"

    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junit_vintage_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_jupiter_version}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junit_platform_version}"

    testCompile "org.assertj:assertj-core:${assertj_version}"

    cordapp project(':samples:irs-demo:cordapp:contracts-irs')
}

cordapp {
    targetPlatformVersion corda_platform_version.toInteger()
    minimumPlatformVersion 1
    workflow {
        name "Corda IRS Demo"
        versionId 1
        vendor "R3"
        licence "Open Source (Apache 2)"
    }
}

jar {
    baseName 'corda-irs-demo-workflows'
}

task testJar(type: Jar) {
    classifier "test"
    from sourceSets.main.output
    from sourceSets.test.output
}

artifacts {
    demoArtifacts testJar
}
