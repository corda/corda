apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'

repositories {
    maven { url "http://ci-artifactory.corda.r3cev.com/artifactory/freighter-dev" }
}
sourceSets {
    freighterTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/freighterTest/kotlin')
        }
    }
}
//WHY are we doing this?? we need to depend only on the workflows / contracts jar themselves as otherwise they will drag in corda internals
//to do this, we must depend on the actual output jar file from the jar task from these projects
evaluationDependsOn(":business-networks:workflows")
evaluationDependsOn(":business-networks:contracts")
evaluationDependsOn(":business-networks:freighter-tests-cordapp")

//force recompile of jars if they've changed before compiling our tests
freighterTestClasses {
    dependsOn([
            project(":business-networks:workflows").jar,
            project(":business-networks:contracts").jar,
            project(":business-networks:freighter-tests-cordapp").jar
    ])
}

task freighterTest(type: Test) {
    maxParallelForks 2
    systemProperty "java.util.concurrent.ForkJoinPool.common.parallelism", "128"
    testClassesDirs = sourceSets.freighterTest.output.classesDirs
    classpath = sourceSets.freighterTest.runtimeClasspath
    useJUnitPlatform {
        includeTags "AZURE"
        excludeTags "ORACLE","DOCKER"
    }
}
configurations {
    freighterTestCompile.extendsFrom testCompile
    freighterTestRuntime.extendsFrom testRuntime
}
dependencies {
    testCompile "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    testCompile "org.slf4j:jul-to-slf4j:$slf4j_version"
    freighterTestCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    freighterTestCompile "freighter:freighter-testing-core-junit5:0.5.0-SNAPSHOT"
    //depend on jar files, not projects - this prevents core and other internal things being dragged into our sandbox
    freighterTestCompile files(
            project(":business-networks:workflows").jar,
            project(":business-networks:contracts").jar,
            project(":business-networks:freighter-tests-cordapp").jar
    )
}
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.2"
        apiVersion = "1.2"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}