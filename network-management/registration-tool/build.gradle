/*
 * R3 Proprietary and Confidential
 *
 * Copyright (c) 2018 R3 Limited.  All rights reserved.
 *
 * The intellectual and technical concepts contained herein are proprietary to R3 and its suppliers and are protected by trade secret law.
 *
 * Distribution of this file or any portion thereof via any medium without the express permission of R3 is strictly prohibited.
 */

apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'us.kirchmeier.capsule'
apply plugin: 'kotlin'

description 'Network registration tool'

version project(':network-management').version

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    jcenter()
    maven {
        url 'http://ci-artifactory.corda.r3cev.com/artifactory/corda-dev'
    }
    maven {
        url 'http://ci-artifactory.corda.r3cev.com/artifactory/corda-releases'
    }
}

configurations {
    runtimeArtifacts.extendsFrom runtime
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task buildRegistrationTool(type: FatCapsule, dependsOn: 'jar') {
    group = "build"
    applicationClass 'com.r3.corda.networkmanage.registration.MainKt'
    archiveName "registration-tool-${version}.jar"
    capsuleManifest {
        applicationVersion = corda_release_version
        systemProperties['visualvm.display.name'] = 'Corda Network Registration Tool'
        minJavaVersion = '1.8.0'
    }
    applicationSource = files(
            project(':network-management:registration-tool').configurations.runtime,
            project(':network-management:registration-tool').jar
    )
}

artifacts {
    runtimeArtifacts buildRegistrationTool
    publish buildRegistrationTool
}

jar {
    classifier "ignore"
}

publish {
    name 'registration-tool'
    disableDefaultJar = true
}

dependencies {
    compile project(':node')
    // TODO: remove this when ArgsParser is moved into corda.
    compile project(':network-management')
    testCompile 'junit:junit:4.12'
    testCompile "org.assertj:assertj-core:${assertj_version}"
}
