apply plugin: 'groovy'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

dependencies {
    compile gradleApi()
    compile localGroovy()
}

repositories {
    mavenCentral()
}

bintray {
    user = System.getenv('CORDA_BINTRAY_USER')
    key = System.getenv('CORDA_BINTRAY_KEY')
    publications = ['quasarUtils']
    dryRun = false
    pkg {
        repo = 'corda'
        name = 'quasar-utils'
        userOrg = 'r3'
        licenses = ['Apache-2.0']
    }
}

task(install, dependsOn: 'publishToMavenLocal')

publishing {
    publications {
        quasarUtils(MavenPublication) {
            from components.java
            groupId  'net.corda.plugins'
            artifactId 'quasar-utils'

            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'quasar-utils'
                    description 'A small gradle plugin for adding some basic Quasar tasks and configurations to reduce build.gradle bloat.'
                    url 'https://github.com/corda/corda'
                    scm {
                        url 'https://github.com/corda/corda'
                    }

                    licenses {
                        license {
                            name 'Apache-2.0'
                            url 'https://www.apache.org/licenses/LICENSE-2.0'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}
