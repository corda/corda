// This script exists just to allow bootstrapping the gradle plugins if maven central or jcenter are unavailable
// or if you are developing these plugins. See the readme for more information.

buildscript {
    // For sharing constants between builds
    Properties constants = new Properties()
    file("$projectDir/../constants.properties").withInputStream { constants.load(it) }

    // If you bump this version you must re-bootstrap the codebase. See the README for more information.
    ext.gradle_plugins_version = constants.getProperty("gradlePluginsVersion")
    ext.bouncycastle_version = constants.getProperty("bouncycastleVersion")
    ext.typesafe_config_version = constants.getProperty("typesafeConfigVersion")

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "net.corda.plugins:publish-utils:$gradle_plugins_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'net.corda.plugins.publish-utils'

allprojects {
    version gradle_plugins_version
    group 'net.corda.plugins'
}

bintrayConfig {
    user = System.getenv('CORDA_BINTRAY_USER')
    key = System.getenv('CORDA_BINTRAY_KEY')
    repo = 'corda'
    org = 'r3'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/corda/corda'
    projectUrl = 'https://github.com/corda/corda'
    gpgSign = true
    gpgPassphrase = System.getenv('CORDA_BINTRAY_GPG_PASSPHRASE')
    publications = ['cordformation', 'quasar-utils', 'cordform-common', 'api-scanner']
    license {
        name = 'Apache-2.0'
        url = 'https://www.apache.org/licenses/LICENSE-2.0'
        distribution = 'repo'
    }
    developer {
        id = 'R3'
        name = 'R3'
        email = 'dev@corda.net'
    }
}
