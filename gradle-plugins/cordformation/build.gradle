buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'java-gradle-plugin'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'com.jfrog.artifactory'

description 'A small gradle plugin for adding some basic Quasar tasks and configurations to reduce build.gradle bloat.'

repositories {
    mavenCentral()
}

configurations {
    noderunner
    compile.extendsFrom noderunner
}

sourceSets {
    runnodes {
        kotlin {
            srcDir file('src/noderunner/kotlin')
            compileClasspath += configurations.noderunner
        }
    }
}

dependencies {
    gradleApi()

    compile project(":cordapp")
    compile project(':cordform-common')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "commons-io:commons-io:2.6"

    noderunner "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    testCompile "junit:junit:4.12" // TODO: Unify with core
    testCompile "org.assertj:assertj-core:3.8.0"
    // Docker-compose file generation
    compile "org.yaml:snakeyaml:$snake_yaml_version"
}

task createNodeRunner(type: Jar) {
    manifest {
        attributes('Main-Class': 'net.corda.plugins.NodeRunnerKt')
    }
    classifier = 'fatjar'
    from { configurations.noderunner.collect { it.isDirectory() ? it : zipTree(it) } }
    from sourceSets.runnodes.output
}

publish {
    name project.name
}

processResources {
    from(createNodeRunner) {
        rename { 'net/corda/plugins/runnodes.jar' }
    }
}
